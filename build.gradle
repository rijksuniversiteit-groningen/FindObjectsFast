apply plugin: "java"
apply plugin: "war"
apply plugin: "maven"

apply from: "b-deploy.gradle"
apply from: "b-findbugs.gradle"
apply from: "b-test.gradle"
apply from: "b-selenium.gradle"

// Learn upto (including) 2015.10 requires compilation with Java 7
sourceCompatibility = 1.7


repositories {
  mavenCentral()
  maven {
    url "https://maven.blackboard.com/content/repositories/releases/"
  }
}



// define the project's dependencies
dependencies {
  compile "com.intellij:annotations:12.0",
          "com.google.code.findbugs:annotations:3.0.0"

  providedCompile("blackboard.platform:bb-platform:${learnVersion}") { transitive = false }

  providedCompile "javax.servlet:javax.servlet-api:3.0.1",
          "javax.servlet.jsp:jsp-api:2.2",
          "taglibs:standard:1.1.2",
          "com.google.guava:guava:17.0"

  compile("blackboard.platform:bb-spring-webapi:10.5") { transitive = false }

  compile "org.springframework:spring-web:3.1.1.RELEASE",
      "org.springframework:spring-webmvc:3.1.1.RELEASE",
      "org.springframework:spring-core:3.1.1.RELEASE"


  compile group: 'org.codehaus.jackson', name: 'jackson-mapper-asl', version: '1.9.10'

}


task parseBbManifest() {
  def manifestFile = new File('src/main/webapp/WEB-INF/bb-manifest.xml')
  if (manifestFile.exists()) {
    def bbManifest = (new XmlParser()).parse(manifestFile)
    version = bbManifest.plugin.version.@value[0]
    def handle = bbManifest.plugin.handle.@value[0]
    def vendor = bbManifest.plugin.vendor.id.@value[0]
    war.baseName = vendor
    war.appendix = handle
    war.destinationDir = new File('build')
  }
}

war.dependsOn parseBbManifest


task wrapper(type: Wrapper) {
  gradleVersion = '2.12'
}

